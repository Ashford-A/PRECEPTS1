
import os
import sys
sys.path.extend([os.path.join(os.environ['CODEDIR'], 'dryads-research',
                              'experiments', 'utilities')])
from pipeline_setup import get_task_arr, tasks_files


TMPDIR = os.path.join(
    os.environ['TEMPDIR'], 'dryads-research', 'gene_isolate',
    config['gene'], config['cohort'],
    config['mut_lvls'], config['search'], config['classif']
    )

OUTDIR = os.path.join(os.environ['DATADIR'],
                      'dryads-research', 'gene_isolate', config['gene'])


localrules: target, merge


rule target:
    input:
        expand("{outdir}/out-conf__"
               "{cohort}__{mut_lvls}_{search}_{classif}.p.gz",
               outdir=OUTDIR, **config)

    threads: 1


rule isolate:
    output: "{TMPDIR}/output/out__cv-{cv_id}_task-{task_id}.p"

    threads: 8

    shell: """
        set +u; source activate research; set -u;

        export OMP_NUM_THREADS=1;
        sleep $(( ({wildcards.cv_id} + 1) * ({wildcards.task_id} + 1) \
                * $(shuf -i 1-9 -n 1) ));

        python -m dryads-research.experiments.gene_isolate.fit_isolate \
                {config[gene]} {config[classif]} {TMPDIR} \
                --task_id={wildcards.task_id} --cv_id={wildcards.cv_id}

        """


rule gather:
    input: tasks_files

    output: "{TMPDIR}/merge/out-conf_{tasks}.p.gz"

    threads: 12

    shell: """
        set +u; source activate research; set -u;

        tasks={wildcards.tasks}
        python -m dryads-research.experiments.gene_isolate.gather_isolate \
                {config[gene]} {TMPDIR} --task_ids ${{tasks//-/ }}

        """


rule merge:
    input:
        [os.path.join(TMPDIR, 'merge',
                      "out-conf_{}.p.gz".format('-'.join(task_list)))
         for task_list in get_task_arr(TMPDIR)]

    output:
        expand("{outdir}/out-conf__"
               "{cohort}__{mut_lvls}_{search}_{classif}.p.gz",
               outdir=OUTDIR, **config)

    threads: 1

    shell:"""
        set +u; source activate research; set -u;
        python -m dryads-research.experiments.subgrouping_isolate.merge_isolate \
                {TMPDIR} --mean --ex_lbls Iso IsoShal

        out_tag={config[cohort]}__{config[mut_lvls]}_{config[search]}_{config[classif]}
        cp {TMPDIR}/setup/cohort-data.p.gz \
                {OUTDIR}/cohort-data__${{out_tag}}.p.gz

        cp {TMPDIR}/out-pred_Iso.p.gz {OUTDIR}/out-pred_Iso__${{out_tag}}.p.gz
        cp {TMPDIR}/out-pred_IsoShal.p.gz \
                {OUTDIR}/out-pred_IsoShal__${{out_tag}}.p.gz

        cp {TMPDIR}/out-tune.p.gz {OUTDIR}/out-tune__${{out_tag}}.p.gz
        cp {TMPDIR}/out-pheno.p.gz {OUTDIR}/out-pheno__${{out_tag}}.p.gz
        cp {TMPDIR}/out-aucs.p.gz {OUTDIR}/out-aucs__${{out_tag}}.p.gz
        cp {TMPDIR}/out-conf.p.gz {OUTDIR}/out-conf__${{out_tag}}.p.gz

        """

