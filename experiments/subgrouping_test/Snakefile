
import os
import sys
sys.path.extend([os.path.join(os.environ['CODEDIR'], 'dryads-research',
                              'experiments', 'utilities')])
from pipeline_setup import get_task_arr, tasks_files


TMPDIR = os.path.join(os.environ['TEMPDIR'], 'dryads-research',
                      'subgrouping_test', config['expr_source'],
                      "{}__samps-{}".format(config['cohort'],
                                            config['samp_cutoff']),
                      config['mut_levels'], config['classif'])

OUTDIR = os.path.join(os.environ['DATADIR'], 'dryads-research',
                      'subgrouping_test',
                      "{}__{}__samps-{}".format(config['expr_source'],
                                                config['cohort'],
                                                config['samp_cutoff']))


localrules: target, merge


rule target:
    input:
        expand("{outdir}/out-trnsf__{mut_levels}__{classif}.p.gz",
               outdir=OUTDIR, **config)

    threads: 1


rule test:
    output: "{TMPDIR}/output/out__cv-{cv_id}_task-{task_id}.p"

    threads: 8

    shell: """
        set +u; source activate research; set -u;

        export OMP_NUM_THREADS=1;
        sleep $(( ({wildcards.cv_id} + 1) * ({wildcards.task_id} + 1)
                * $(shuf -i 1-9 -n 1) ));

        python -m dryads-research.experiments.subgrouping_test.fit_test \
                {config[classif]} {TMPDIR} \
                --task_id={wildcards.task_id} --cv_id={wildcards.cv_id}

        """


rule gather:
    input: tasks_files

    output: "{TMPDIR}/merge/out-trnsf_{tasks}.p.gz",

    threads: 12

    shell: """
        set +u; source activate research; set -u;

        tasks={wildcards.tasks}
        python -m dryads-research.experiments.subgrouping_test.gather_test \
                {TMPDIR} --task_ids ${{tasks//-/ }}

        """


rule merge:
    input:
        [os.path.join(TMPDIR, 'merge',
                      "out-trnsf_{}.p.gz".format('-'.join(task_list)))
         for task_list in get_task_arr(TMPDIR)]

    output:
        expand("{outdir}/out-trnsf__{mut_levels}__{classif}.p.gz",
               outdir=OUTDIR, **config)

    threads: 1

    shell:"""
        set +u; source activate research; set -u;
        python -m dryads-research.experiments.subgrouping_test.merge_test \
                {TMPDIR}

        out_tag={config[mut_levels]}__{config[classif]}
        cp {TMPDIR}/setup/cohort-data.p.gz \
                {OUTDIR}/cohort-data__${{out_tag}}.p.gz

        cp {TMPDIR}/out-pred.p.gz {OUTDIR}/out-pred__${{out_tag}}.p.gz
        cp {TMPDIR}/out-coef.p.gz {OUTDIR}/out-coef__${{out_tag}}.p.gz
        cp {TMPDIR}/out-tune.p.gz {OUTDIR}/out-tune__${{out_tag}}.p.gz
        cp {TMPDIR}/out-pheno.p.gz {OUTDIR}/out-pheno__${{out_tag}}.p.gz
        cp {TMPDIR}/out-aucs.p.gz {OUTDIR}/out-aucs__${{out_tag}}.p.gz
        cp {TMPDIR}/out-conf.p.gz {OUTDIR}/out-conf__${{out_tag}}.p.gz

        cp {TMPDIR}/trnsf-preds.p.gz {OUTDIR}/trnsf-preds__${{out_tag}}.p.gz
        cp {TMPDIR}/out-trnsf.p.gz {OUTDIR}/out-trnsf__${{out_tag}}.p.gz

        """

