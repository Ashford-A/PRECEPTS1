
import os


LOCALDIR = os.path.join(os.environ['CODEDIR'],
                        'HetMan', 'experiments', 'subgrouping_isolate')

TMPDIR = os.path.join(
    os.environ['TEMPDIR'], 'HetMan', 'subgrouping_isolate',
    config['expr_source'], config['cohort'],
    config['mut_levels'], config['search'], config['classif']
    )

OUTDIR = os.path.join(os.environ['DATADIR'],
                      'HetMan', 'subgrouping_isolate',
                      '__'.join([config['expr_source'], config['cohort']]))


tasks_file = open(os.path.join(TMPDIR, 'setup', "tasks.txt"), 'r')
tasks_list = tasks_file.readlines()
tasks_file.close()

task_arr = [tasks.strip().split(' ') for tasks in tasks_list]
task_arr = [[tsks[i] for tsks in task_arr if i < len(tsks)]
            for i in range(len(task_arr[0]))]


localrules: target, merge


rule target:
    input:
        expand("{outdir}/out-siml__{mut_levels}__{search}__{classif}.p.gz",
               outdir=OUTDIR, **config)

    threads: 1


rule isolate:
    output: "{TMPDIR}/output/out__cv-{cv_id}_task-{task_id}.p"

    threads: 8

    shell: """
        set +u; source activate HetMan; set -u;
        export OMP_NUM_THREADS=1;
        sleep $(( ({wildcards.cv_id} + 1) * ({wildcards.task_id} + 1) \
                * $(shuf -i 1-9 -n 1) ));

        task_count=$(( 1 + $( grep -Eo '[0-9]+' \
                {TMPDIR}/setup/tasks.txt | sort -rn | head -n 1 ) ))

        python -m HetMan.experiments.subgrouping_isolate.fit_isolate \
                {config[classif]} {TMPDIR} --task_count=$task_count \
                --task_id={wildcards.task_id} --cv_id={wildcards.cv_id}

        """


def tasks_files(wildcards):
    return [os.path.join(TMPDIR, 'output',
                         "out__cv-{}_task-{}.p".format(cv_id, task_id))
            for cv_id in range(40) for task_id in wildcards.tasks.split('-')]


rule gather:
    input: tasks_files

    output: "{TMPDIR}/merge/out-siml_{tasks}.p.gz"

    threads: 12

    shell: """
        set +u; source activate HetMan; set -u;

        tasks={wildcards.tasks}
        python -m HetMan.experiments.subgrouping_isolate.gather_isolate \
                {TMPDIR} --task_ids ${{tasks//-/ }}

        """


rule merge:
    input:
        [os.path.join(TMPDIR, 'merge',
                      "out-siml_{}.p.gz".format('-'.join(task_list)))
         for task_list in task_arr]

    output:
        expand("{outdir}/out-siml__{mut_levels}__{search}__{classif}.p.gz",
               outdir=OUTDIR, **config)

    threads: 1

    shell:"""
        set +u; source activate HetMan; set -u;
        python -m HetMan.experiments.subgrouping_isolate.merge_isolate \
                {TMPDIR}
        out_tag={config[mut_levels]}__{config[search]}__{config[classif]}

        cp {TMPDIR}/setup/cohort-data.p.gz \
                {OUTDIR}/cohort-data__${{out_tag}}.p.gz
        cp {TMPDIR}/out-pred.p.gz {OUTDIR}/out-pred__${{out_tag}}.p.gz
        cp {TMPDIR}/out-tune.p.gz {OUTDIR}/out-tune__${{out_tag}}.p.gz
        cp {TMPDIR}/out-pheno.p.gz {OUTDIR}/out-pheno__${{out_tag}}.p.gz

        cp {TMPDIR}/out-aucs.p.gz {OUTDIR}/out-aucs__${{out_tag}}.p.gz
        cp {TMPDIR}/out-conf.p.gz {OUTDIR}/out-conf__${{out_tag}}.p.gz
        cp {TMPDIR}/out-siml.p.gz {OUTDIR}/out-siml__${{out_tag}}.p.gz

        """

